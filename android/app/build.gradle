plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

// Flavor-specific Maps API Keys
def chaimatesMapsApiKey = localProperties.getProperty('CHAIMATES_MAPS_API_KEY') ?: System.getenv('CHAIMATES_MAPS_API_KEY') ?: ""
def jdsKitchenMapsApiKey = localProperties.getProperty('JDS_KITCHEN_MAPS_API_KEY') ?: System.getenv('JDS_KITCHEN_MAPS_API_KEY') ?: ""

if (!chaimatesMapsApiKey) {
    println "Warning: Chaimates Maps API key not set. Add CHAIMATES_MAPS_API_KEY to local.properties"
}
if (!jdsKitchenMapsApiKey) {
    println "Warning: JD's Kitchen Maps API key not set. Add JDS_KITCHEN_MAPS_API_KEY to local.properties"
}

android {
    namespace "com.example.outlet_app"
    compileSdk flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.example.outlet_app"
        minSdkVersion flutter.minSdkVersion
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        multiDexEnabled true
        manifestPlaceholders = [
                applicationName: "io.flutter.app.FlutterApplication"
        ]
    }

    flavorDimensions "brand"

    productFlavors {
        chaimates {
            dimension "brand"
            applicationId "com.saas_outlet_app.chaimates"
            resValue "string", "app_name", "Chaimates - Outlet"
            manifestPlaceholders = [
                    applicationName: "io.flutter.app.FlutterApplication",
                    MAPS_API_KEY : chaimatesMapsApiKey
            ]
        }

        jds_kitchen {
            dimension "brand"
            applicationId "com.saas_outlet_app.jds_kitchen"
            resValue "string", "app_name", "JD\\'s Kitchen - Outlet"
            manifestPlaceholders = [
                    applicationName: "io.flutter.app.FlutterApplication",
                    MAPS_API_KEY : jdsKitchenMapsApiKey
            ]
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.debug
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:33.1.2')
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.multidex:multidex:2.0.1'
}
